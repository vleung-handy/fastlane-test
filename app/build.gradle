import org.gradle.logging.StyledTextOutputFactory

import static org.gradle.logging.StyledTextOutput.Style

buildscript {
    repositories {
        flatDir {
            dirs 'libs'
            dirs 'aars'
        }
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.3.2'
        classpath 'io.fabric.tools:gradle:1.21.5'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.4.3'
    }
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://dl.bintray.com/urbanairship/android" }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'


apply plugin: 'io.fabric'
apply plugin: 'newrelic'
apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: 'jacoco.gradle'

def computeVersionCode(versionName) {
    def versionNameMatcher = (versionName =~ /^([1-9]\d*)\.(\d{1,2})\.(\d{1,2})$/)
    if (versionNameMatcher.matches()) {
        def identifiers = (versionNameMatcher[0] - [versionName])
        def (major, minor, patch) = identifiers.collect { it.toInteger() }
        def versionCode = (major * 10000) + (minor * 100) + (patch * 1)

        def out = services.get(StyledTextOutputFactory).create("debug")
        out.withStyle(Style.Info).println("\nBuilding version ${versionName} (${versionCode})\n")
        return versionCode
    } else {
        throw new InvalidUserDataException("Illegal version name ${versionName}")
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'
    testBuildType "debug"

    defaultConfig {
        multiDexEnabled true
        applicationId "com.handy.portal"
        minSdkVersion 16
        targetSdkVersion 23
        versionName '1.0.3'
        versionCode computeVersionCode(versionName)
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias 'HandyProdKey'
            keyPassword 'S0H@ndy!'
            storeFile file('keys/handy-prod-key.keystore')
            storePassword 'S0H@ndy!'
        }
        prod {
            keyAlias 'HandyProdKey'
            keyPassword 'S0H@ndy!'
            storeFile file('keys/handy-prod-key.keystore')
            storePassword 'S0H@ndy!'
        }
        playstore {
            keyAlias 'HandyReleaseKey'
            keyPassword 'S0H@ndy!'
            storeFile file('keys/handy-release-key.keystore')
            storePassword 'S0H@ndy!'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    productFlavors {
        hosted {
            applicationId "com.handy.portal"
            manifestPlaceholders = [PACKAGE_NAME: "com.handy.portal"]
            versionName defaultConfig.versionName
            signingConfig signingConfigs.prod
        }
        store {
            applicationId 'com.handybook.handy_portal'
            manifestPlaceholders = [PACKAGE_NAME: "com.handybook.handy_portal"]
            versionName defaultConfig.versionName + "p"
            signingConfig signingConfigs.playstore
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def buildType = variant.buildType.name == 'release' ? '' : '-' + variant.buildType.name;
                output.outputFile = new File(output.outputFile.parentFile,
                        "handy-pro${buildType}-${variant.versionName}.apk");
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile files('libs/YouTubeAndroidPlayerApi-1.2.2.jar')

    //Multidexing support
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'

    //Recommended for in-app messaging
    compile 'com.android.support:design:23.3.0'

    //Google services
    compile 'com.google.android.gms:play-services-location:9.0.2'
    //Recommended for location services
    compile 'com.google.android.gms:play-services-gcm:9.0.2'
    //Required for Android (GCM) push notifications

    //Guava
    compile 'com.google.guava:guava:18.0'

    //Square libraries, butterknife, dagger, retrofit, picasso
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton:butterknife:7.0.1'

    // Signature for checkout
    compile 'com.github.gcacace:signature-pad:1.1.0'

    // For better visualizing circular graphs
    compile 'com.github.bmarrdev:android-DecoView-charting:v0.9.5'

    //Mixpanel analytics
    compile 'com.mixpanel.android:mixpanel-android:4.6.4@aar'

    //Custom font display
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    //Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    //New relic crash reporting
    compile 'com.newrelic.agent.android:android-agent:5.3.2'

    //SecurePrefs storage
    compile 'com.scottyab:secure-preferences-lib:0.1.4'

    //Urban Airship SDK
    compile 'com.urbanairship.android:urbanairship-sdk:6.3.0'

    //particle system/animations
    compile 'com.plattysoft.leonids:LeonidsLib:1.3.1'

    // Sticky list header
    compile 'se.emilsjolander:stickylistheaders:2.7.0'

    //Stripe payment processing
    compile 'com.stripe:stripe-android:1.0.4'

    //Testing
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    //Espresso libraries for automation testing
    androidTestCompile ('com.android.support.test:runner:0.4.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}
