buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.1.2'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.7'
    }
}
repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://dl.bintray.com/urbanairship/android" }
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'newrelic'
apply plugin: 'jacoco'

apply from: 'jacoco.gradle'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.handy.portal"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 24
        versionName "0.5.0"
        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    signingConfigs {
        prod {
            keyAlias 'HandyProdKey'
            keyPassword 'S0H@ndy!'
            storeFile file('keys/handy-prod-key.keystore')
            storePassword 'S0H@ndy!'
        }
        playstore {
            keyAlias 'HandyReleaseKey'
            keyPassword 'S0H@ndy!'
            storeFile file('keys/handy-release-key.keystore')
            storePassword 'S0H@ndy!'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    productFlavors {
        stable {
            applicationId "com.handy.portal"
            manifestPlaceholders = [PACKAGE_NAME: "com.handy.portal"]
            versionName "0.5.0"
            signingConfig signingConfigs.prod
        }
        beta {
            applicationId "com.handy.portal"
            manifestPlaceholders = [PACKAGE_NAME: "com.handy.portal"]
            versionName "0.5.0b"
            signingConfig signingConfigs.prod
        }
        store {
            applicationId 'com.handybook.handy_portal'
            manifestPlaceholders = [PACKAGE_NAME: "com.handybook.handy_portal"]
            versionName "0.5.0p"
            signingConfig signingConfigs.playstore
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def buildType = variant.buildType.name == 'release' ? '' : '-' + variant.buildType.name;
                output.outputFile = new File(output.outputFile.parentFile,
                        "handy-pro${buildType}-${variant.versionName}.apk");
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Multidex support
    compile 'com.android.support:multidex:1.0.0'

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'

    //Google play services
    compile 'com.google.android.gms:play-services:7.5.0'

    //Guava
    compile 'com.google.guava:guava:18.0'

    //Square libraries, butterknife, dagger, retrofit
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    //Mixpanel analytics
    compile 'com.mixpanel.android:mixpanel-android:4.4.1@aar'

    //Custom font display
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    //Crashyltics client crash reporting
    compile 'com.crashlytics.android:crashlytics:1.1.13'

    //New relic crash reporting
    compile 'com.newrelic.agent.android:android-agent:5.1.2'

    //SecurePrefs storage
    compile 'com.scottyab:secure-preferences-lib:0.1.1'

    // Urban Airship SDK
    compile 'com.urbanairship.android:urbanairship-sdk:6.1.+'

    // Recommended for in-app messaging
    compile 'com.android.support:cardview-v7:22.2.0'

    // Recommended for location services
    compile 'com.google.android.gms:play-services-location:7.5.0'

    // Required for Android (GCM) push notifications
    compile 'com.google.android.gms:play-services-gcm:7.5.0'

    //Testing
    testCompile 'org.robolectric:robolectric:3.0-rc3'
    testCompile 'org.robolectric:shadows-support-v4:3.0-rc3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'


}
